# 存储过程

## 实验介绍

### 实验内容



### 实验知识点

+ ​




## 存储过程简介

一个过程是可以通过名字调用的一组 PL/SQL 语句。它将复杂的业务规则从应用程序中分离出来交给数据库，应用程序只需要调用存储过程获取返回的结果就行了。这种方式将复杂的数据处理交给了服务器处理，大大提高了效率，并且后期维护会很容易，因为不用在大量的应用程序代码中修改，而只需要修改存储过程。

## 创建存储过程

创建存储过程必须拥有 `CREATE PROCEDURE` 系统权限或者 `CREATE ANY PROCEDURE` 系统权限。后者用于在其他用户模式中创建存储过程。

创建存储过程的具体语法可参见 [创建存储过程](https://docs.oracle.com/en/database/oracle/oracle-database/12.2/sqlrf/CREATE-PROCEDURE.html#GUID-771879D8-BBFD-4D87-8A6C-290102142DA3) 。我们可以创建无参和有参存储过程，下面从实践中去体会它们的实际运用。

### 创建无参存储过程

接下来我们来创建一个简单的存储过程，实现打印 `hello world` 。

首先确定 SERVEROUTPUT 是否打开：

```sql
SQL> show serveroutput;

--如果显示未打开，则使用如下方式打开
SQL> set serveroutput on;
```

接着创建一个名为 `pro1` 的存储过程：

```plsql
CREATE OR REPLACE PROCEDURE pro1     --创建无参存储过程
AS
BEGIN
	DBMS_OUTPUT.PUT_LINE('hello world');
END;
/
```

我们可以查看是否创建成功：

```plsql
select * from user_source where name='PRO1';
```

> `user_source` 是查询当前用户的存储过程。如果要查看所有的存储过程使用 `all_source` 。
>
> `OR REPLACE` 表示如果过程已经存在，则同名的存储过程。

下面执行一下我们创建的这个存储过程：

```plsql
exec pro1;

--或者使用如下方式执行
BEGIN
pro1;
END;
/
```

输出结果：

```
PL/SQL 过程已成功完成。
hello world
```

### 创建有参存储过程

创建有参的存储过程的概念就像其他程序语言的创建带参函数，以便于数据的输入输出。创建存储过程中定义的参数有输入，输出，输入输出三种参数。下面通过实践分别学习创建带各种参数的存储过程。

#### 输入类型参数

我们之前有写过一个根据键盘输入的学生编号查找学生姓名的 PL/SQL 程序。在这里我们调用存储过程，向其传入学生编号，返回学生姓名的方式实现姓名查询。

```plsql
CREATE PROCEDURE PRO_NAME(arg_id IN NUMBER)
AS
  v_sname VARCHAR2(20);    --接收学生姓名
BEGIN
  SELECT s_name INTO v_sname FROM student WHERE s_id=arg_id; --把查询出来的值赋给变量 v_sname
  DBMS_OUTPUT.put_line('student''s name is : ' || v_sname);
END;
/
```

> `IN` 代表定义的参数是输入参数，是默认的，可以省略 IN。`NUMBER` 是输入参数的类型。
>
> `AS` 后面声明了一个变量用来接收学生姓名，用于打印出查询出来的学生姓名。
>
> `BEGIN` 和 `END` 之间是执行的查询操作。

查询学生编号为 1001 的学生姓名：

```plsql
exec PRO_NAME(1001);
```

输出结果：

```plsql
PL/SQL 过程已成功完成。
student's name is : shiyanlou1001
```

#### 输出类型参数

如果有其他存储过程想使用查询出来的学生姓名怎么办呢。这个时候就需要在被调用的存储过程中定义输出参数，将接收学生姓名的参数传出来供其他存储过程使用。首先我们创建一个被调用的存储过程，它能根据传入的学生编号，将学生姓名传出。

```plsql
CREATE PROCEDURE PRO_NAME2(arg_id IN NUMBER,arg_name OUT VARCHAR2)
AS
BEGIN
  SELECT s_name INTO arg_name FROM student WHERE s_id=arg_id;   --把查询出来的值给输入参数 arg_name
END;
/
```

> `OUT` 代表定义的参数是输出参数。

然后创建一个存储过程，它调用上面的存储过程，然后将学生姓名打印出来：

```plsql
CREATE PROCEDURE PRO_GETNAME
AS
  v_name student.s_name%TYPE;   --接收学生姓名
BEGIN
  PRO_NAME2(1001,v_name);     --调用存储过程 PRO_NAME2，将传出的学生姓名给变量 v_name
  DBMS_OUTPUT.put_line('student''s name is : ' || v_name);  --打印出变量 v_name 的值
END;
/
```

执行存储过程 PRO_GETNAME 及其输出结果：

```plsql
SQL> exec pro_getname;

PL/SQL 过程已成功完成。
student's name is : shiyanlou1001
```

#### 输入输出类型参数

顾名思义，也就是既可以输入也可以输出。例如我们根据学生编号查询对应的学生年龄。首先创建被调用的存储过程：

```plsql
CREATE PROCEDURE PRO_NAME3(arg_id_age IN OUT NUMBER)  --定义输入输出参数，它既可以传入学生编号，又可以传出学生年龄
AS
BEGIN
	--根据学生编号查询学生年龄
  SELECT s_age INTO arg_id_age FROM student WHERE s_id=arg_id_age; 
END;
/
```

然后创建一个存储过程调用上面的存储过程，查询学生编号为 1001 的学生年龄。

```plsql
CREATE PROCEDURE PRO_GETAGE
AS
  v_id_age student.s_age%TYPE;  
BEGIN
  v_id_age:=1001;     --学生编号为 1001
  PRO_NAME3(v_id_age);  --调用 PRO_NAME3 存储过程，查询出 1001 学生的年龄，再给变量 v_id_age
  DBMS_OUTPUT.put_line('student''s age is : ' || v_id_age);  --打印出学生年龄
END;
/
```

执行存储过程以及输出结果：

```plsql
SQL> exec pro_getage;

PL/SQL 过程已成功完成。
student's age is : 10
```

> **注：编译存储过程时难免遇到各种错误，此时可以使用 `show errors procedure <procedure_name>` 查看更详细的错误信息。**

想了解更多有关创建存储过程内容可参考[创建存储过程](https://docs.oracle.com/en/database/oracle/oracle-database/12.2/sqlrf/CREATE-PROCEDURE.html#GUID-771879D8-BBFD-4D87-8A6C-290102142DA3) 。

## 删除存储过程

例：删除存储过程 pro1 。

```sql
SQL> drop procedure pro1;

--查询可发现已经没有 pro1 这个存储过程。
SQL> select * from user_source where name='PRO1';
```

## 存储过程中使用 DDL 语句

上面创建的存储过程中使用的都是 DML 语句，如果要使用 DDL 语句，比如创建表等，不能像使用 DML 语句一样直接使用，而需要加上 `EXECUTE IMMEDIATE` 。

例如创建一个表，其中包含学生姓名和学生平均成绩。

```plsql
CREATE PROCEDURE PRO_GRADE
AS
BEGIN
  EXECUTE IMMEDIATE 'CREATE TABLE stu_grade AS select s_name,avg(grade) avg_grade from student join sc using(s_id) group by s_name order by s_name';   --创建表
END;
```

执行存储过程以及查询表 stu_grade 中的内容：

```plsql
exec pro_grade;
select * from stu_grade;
```

## 存储过程中使用游标

之前我们编写的存储过程中的查询基本都是返回一个结果或者一行结果。如果想要返回查询出来的多个结果的话，就可以使用游标。游标就像一个游动的指针，它可以获取结果集中的任意行，这样的话，只要搭配循环语句，一行一行地返回，就可以打印出整个结果集的内容。我们用如下实例来具体理解。

将会使用我们在 SQL 一节学习中创建的 student 示例表。例如我们要打印出不同性别的学生编号，学生名称，学生年龄，并计算不同性别的学生数量。

**思路：**

1. 首先从表中查出有哪些性别。
2. 用游标和循环每次获取一个性别给一个变量。
3. 根据获取的性别，查询出对应的学生。
4. 用 for 循环遍历，输出每个学生信息。
5. 计数就是 count 函数。

**代码：**

```plsql
CREATE OR REPLACE PROCEDURE PRO_SEX    --创建存储过程
AS
v_count NUMBER;         --变量，用于接收学生数量
v_sex student.s_sex%TYPE;   --变量，用户接收学生性别
CURSOR cur_sex      --创建游标
IS
SELECT s_sex FROM student GROUP BY s_sex;  --与游标关联的sql语句，此sql语句是查询出有哪些性别
BEGIN
OPEN cur_sex;      --打开游标

--第一个循环开始，用来循环性别
LOOP
FETCH cur_sex INTO v_sex;    --获取游标当前指向的数据给变量 v_sex，第一次 v_sex 被赋值为 woman，第二次 v_sex 被赋值为 man
EXIT WHEN cur_sex%NOTFOUND;    --当获取不到数据时退出
DBMS_OUTPUT.PUT_LINE('===================');
DBMS_OUTPUT.PUT_LINE(v_sex || ' :');

--第二个循环开始，用来循环查询出的结果集，获取每行数据
FOR row_student IN     --FOR 循环遍历结果中每行数据给变量 row_student 
(
  SELECT * FROM student WHERE s_sex=v_sex  --查询当前获取的性别有哪些学生
)
LOOP
  DBMS_OUTPUT.PUT_LINE(
  'student id: ' || row_student.s_id ||      --打印学生编号
  ' student name: '|| row_student.s_name ||  --打印学生姓名
  ' student age: ' || row_student.s_age      --打印学生年龄
  );
END LOOP;  --第二个循环结束

SELECT count(s_id) INTO v_count FROM student WHERE s_sex=v_sex;   --查询出当前获取的性别的学生数量
IF SQL%FOUND THEN     --如果此查询有结果就执行下面的语句
  DBMS_OUTPUT.PUT_LINE('count: ' || v_count);   --打印数量
END IF;

END LOOP;  --第一个循环结束
CLOSE cur_sex;    --关闭游标
END;
/
```

执行以及输出结果：

```plsql
SQL> exec pro_sex;

PL/SQL 过程已成功完成。
===================
woman :
student id: 1002 student name: shiyanlou1002 student age: 20
student id: 1004 student name: shiyanlou1004 student age: 40
count: 2
===================
man :
student id: 1001 student name: shiyanlou1001 student age: 10
student id: 1003 student name: shiyanlou1003 student age: 18
student id: 1005 student name: shiyanlou1005 student age: 17
count: 3
```

**补充：**

- ```plsql
  CURSOR cur_sex
  IS
  SELECT s_sex FROM student GROUP BY s_sex;
  ```

  `cur_sex` 是创建的游标名称。

  `IS` 后面是关联的查询语句，游标可以从此查询的结果集中获取某行的数据。注意不能是 `SELECT INTO` 。

  用这种方式创建的游标被称为显式游标，需要手动打开，手动关闭。

- ```plsql
  cur_sex%NOTFOUND
  ```

  这里是使用的显式游标的属性。`%NOTFOUND` 表示当获取不到数据时返回 `TRUE` ，反之返回 `FALSE` 。

  显示游标还有其他属性 `%FOUND` ，`%ISOPEN` ，`%ROWCOUNT` 。可参见 [游标属性](https://docs.oracle.com/en/database/oracle/oracle-database/12.2/lnpls/named-cursor-attribute.html#GUID-CD8D8415-FF19-4D81-99BA-7825FD40CC96) 。                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                               

- ```plsql
  SQL%FOUND 
  ```

  这里是用了一个隐式游标，`SQL` 是隐式游标的默认名称，`%FOUND` 是它的属性，表示 DML 操作是否对数据有影响，比如查询语句是否返回有结果，插入语句是否成功等等。如果有影响则返回 `TURE` ，反之返回 `FALSE` 。

  隐式游标也有和显式游标相同的属性，只是 `%ISOPEN` 永远为 `FALSE` 。

## 存储过程中使用事务

事务是用来保证数据的一致性。一个很常见的例子就是银行转账，转出账户减少金额，转入账户增加金额，肯定不允许有转出成功，转入却失败或者转出失败，转入成功的情况发生。这里整个交易过程就是一个事务，在这个事务中任意一个操作失败就都失败，所有的操作成功，整个交易过程才算成功。如下示例更好地理解事务。

新建一个像表 student 插入两条数据的存储过程：

```plsql
CREATE OR REPLACE PROCEDURE PRO_TRAN
AS
BEGIN
  INSERT INTO student VALUES(1006,'shiyanlou1006','woman',22);
  COMMIT;         --提交事务
  INSERT INTO student VALUES(1007,'shiyanlou1007','woman',22);
END;
/
```

然后查询表中数据：

```plsql
SQL> select * from student;

      S_ID S_NAME               S_SEX                     S_AGE
---------- -------------------- -------------------- ----------
      1001 shiyanlou1001        man                          10
      1002 shiyanlou1002        woman                        20
      1003 shiyanlou1003        man                          18
      1004 shiyanlou1004        woman                        40
      1005 shiyanlou1005        man                          17
      1006 shiyanlou1006        woman                        22
      1007 shiyanlou1007        woman                        22
```

可以看到数据已经插入进去。但是我们在终端新开一个标签，登入同样的账户查询你会发现少了 1007 那条数据：

1. 在终端新开一个标签，然后执行如下语句：

```bash
$ sqlplus system/Syl12345
```

2. 进入 sql 命令行，然后执行如下查询语句：

```sql
SQL> select * from student;

      S_ID S_NAME               S_SEX                     S_AGE
---------- -------------------- -------------------- ----------
      1001 shiyanlou1001        man                          10
      1002 shiyanlou1002        woman                        20
      1003 shiyanlou1003        man                          18
      1004 shiyanlou1004        woman                        40
      1005 shiyanlou1005        man                          17
      1006 shiyanlou1006        woman                        22
```

> 这是因为我们只提交了第一条插入语句的修改结果。我们新开的这个会话只能查询到已经提交的数据。

下面我们再用一个实例说明数据的一致性。回到第一个 sqlplus 窗口。先将开始插入的数据两条数据删除：

```sql
SQL> delete from student where s_id=1006;
SQL> delete from student where s_id=1007;
SQL> commit;
```

然后编写如下存储过程：

```plsql
CREATE OR REPLACE PROCEDURE PRO_TRAN
AS
BEGIN
  INSERT INTO student VALUES(1006,'shiyanlou1006','woman',22);
  INSERT INTO student VALUES(1006,'shiyanlou1007','woman',22);
  COMMIT;
EXCEPTION WHEN OTHERS THEN
  rollback;     --如果出现错误则回滚
END;
/
```

> 这里的 `COMMIT` 表示两条数据都插入成功才提交。

这里故意制造了一个主键错误，我们插入的学生的学生编号相同了。所以都不会插入成功。我们来查询下结果是否如此：

```sql
SQL> select * from student;

      S_ID S_NAME               S_SEX                     S_AGE
---------- -------------------- -------------------- ----------
      1001 shiyanlou1001        man                          10
      1002 shiyanlou1002        woman                        20
      1003 shiyanlou1003        man                          18
      1004 shiyanlou1004        woman                        40
      1005 shiyanlou1005        man                          17
```

**注意：**我们在上面提交的都是 DML 操作，而 DDL 操作（比如创建表等）是自动提交的。

实际上 DML 语句也能自动提交，它跟 `AUTOCOMMIT` 这个参数有关。首先我们来查询一下 `AUTOCOMMIT` 的值：

```sql
SQL> show autocommit;
autocommit OFF
```

可以看到是关闭的。执行如下语句可以开启它，也就是当我们执行 DML 语句时会自动提交了。

```sql
SQL> set autocommit on;
```

接下来编写如下的存储过程来测试一下：

```plsql
CREATE OR REPLACE PROCEDURE PRO_TRAN
AS
BEGIN
  INSERT INTO student VALUES(1006,'shiyanlou1006','woman',22);
  INSERT INTO student VALUES(1007,'shiyanlou1007','woman',22);
END;
/
```

然后执行存储过程：

```sql
SQL> exec pro_tran;
```

到第二个 sqlplus 窗口去查询 student 表中的数据：

```sql
SQL> select * from student;

      S_ID S_NAME               S_SEX                     S_AGE
---------- -------------------- -------------------- ----------
      1001 shiyanlou1001        man                          10
      1002 shiyanlou1002        woman                        20
      1003 shiyanlou1003        man                          18
      1004 shiyanlou1004        woman                        40
      1005 shiyanlou1005        man                          17
      1006 shiyanlou1006        woman                        22
      1007 shiyanlou1007        woman                        22
```

从结果中你会发现已经自动提交了。

上面的事务都比较短，如果一个事务很长，我们想要回滚的时候能够回滚到某一段的话，可以设置不同的保存点。如下示例将学习如何设置保存点以及回滚到某一个保存点。

首先我们将之前插入的数据删除：

```sql
SQL> delete from student where s_id=1006;
SQL> delete from student where s_id=1007;
SQL> commit;
```

然后在第一个 sqlplus 窗口编写如下存储过程：

```plsql
CREATE OR REPLACE PROCEDURE PRO_TRAN
AS
BEGIN
  INSERT INTO student VALUES(1006,'shiyanlou1006','woman',22);
  SAVEPOINT SP1;   --设置第一个保存点
  INSERT INTO student VALUES(1007,'shiyanlou1007','woman',22);
  SAVEPOINT SP2;   --设置第二个保存点
  rollback to SP1;   --回滚到第一个保存点
END;
/
```

执行以及输出结果：

```sql
SQL> select * from student;

      S_ID S_NAME               S_SEX                     S_AGE
---------- -------------------- -------------------- ----------
      1001 shiyanlou1001        man                          10
      1002 shiyanlou1002        woman                        20
      1003 shiyanlou1003        man                          18
      1004 shiyanlou1004        woman                        40
      1005 shiyanlou1005        man                          17
      1006 shiyanlou1006        woman                        22
```

从结果中可以看到只插入了 1006 那行数据。

最后，设置 `AUTOCOMMIT` 的值为 off，并且删除上面插入的数据。

```sql
SQL> set autocommit off;
SQL> delete from student where s_id=1006;
```



## ?函数




## 总结

